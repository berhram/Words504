plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'ru.easycode.words504'
    compileSdk 33

    defaultConfig {
        applicationId "ru.easycode.words504"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "DEEPL_API_KEY", "\"${getDeeplApiKey()}\""
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding = true
    }
    sourceSets {
        test.java.srcDirs += ["${project(':core').projectDir}/src/test/java"]
    }
}

dependencies {

    implementation project(path: ':core')

    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintlayoutVersion"
    testImplementation "junit:junit:$rootProject.junitTest"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$rootProject.kotlinxCoroutinesTest"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.extJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espressoCoreVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$rootProject.lifeCycleVersion"
    implementation "com.google.android.gms:play-services-auth:$rootProject.playServicesVersion"
}

def getDeeplApiKey() {
    def apiKey = System.getenv("DEEPL_API_KEY")
    if (apiKey == null || apiKey.length() == 0) {
        def properties = new Properties()
        rootProject.file("local.properties").withInputStream { properties.load(it) }
        apiKey = properties.DEEPL_API_KEY
    }
    return apiKey
}
